Directory structure:
└── mrfearless-apisearch-plugin-x64/
    ├── README.md
    ├── APISearch-readme.txt
    ├── APISearch.Asm
    ├── APISearch.Def
    ├── APISearch.Inc
    ├── APISearch.dlg
    ├── APISearch.rap
    ├── APISearch.rc
    ├── APISearch.xml
    ├── images/
    ├── release/
    │   ├── APISearch-readme.txt
    │   └── APISearch.dp64
    └── res/
        ├── APISearchDlg.rc
        ├── APISearchRes.rc
        └── APISearchVer.rc

================================================
File: README.md
================================================
# APISearch Plugin (x64) - A Plugin For x64dbg

![](https://github.com/mrfearless/APISearch-Plugin-x64/blob/master/images/APISearch.png) [Current version: 1.0.0.4 - Last updated: 13/01/2017](https://github.com/mrfearless/APISearch-Plugin-x64/releases/latest) For the x86 version of this plugin, visit [here](https://github.com/mrfearless/APISearch-Plugin-x86)

## Overview

A plugin to allow searching for API calls and/or searching online from command bar

## Features

* Search online for API calls in the dissassembly window (lines that begin with 'call')
* Search from the command bar using google, msdn or pinvoke, ie: 'google <searchterm>'
* Open web browser to google, msdn or pinvoke, ie: 'msdn' opens browser at msdn.microsoft.com

## How to install

* If x64dbg (x64dbg 64bit) is currently running, stop and exit.
* Copy the `APISearch.dp64` to your `x64dbg\x64\plugins` folder.
* Start x64dbg

## Information

* Written by [fearless](https://github.com/mrfearless)  - [www.LetTheLight.in](http://www.LetTheLight.in)
* Created with the [x64dbg Plugin SDK For x64 Assembler](https://github.com/mrfearless/x64dbg-Plugin-SDK-For-x64-Assembler)
* A RadASM project (.rap) is used to manage and compile the plugin. The RadASM IDE can be downloaded [here](http://www.softpedia.com/get/Programming/File-Editors/RadASM.shtml)
* The x64 version of this plugin uses [JWasm64](http://masm32.com/board/index.php?topic=3795.0)
* The [JWasm for RadASM package](http://masm32.com/board/index.php?topic=4162.0) is also required to build this x64 version.

## x64dbg
* [x64dbg website](http://x64dbg.com)
* [x64dbg github](https://github.com/x64dbg/x64dbg)
* [x64dbg gitter](https://gitter.im/x64dbg/x64dbg)



================================================
File: APISearch-readme.txt
================================================
;=====================================================================================
;
; APISearch-readme.txt
;
; v1.0.0.4 - Last updated: 13/01/2017
;
;-------------------------------------------------------------------------------------

About
-----

APISearch Plugin (x64) For x64dbg (64bit plugin)
by fearless - www.LetTheLight.in

Created with the x64dbg Plugin SDK For x64 Assembler
https://github.com/mrfearless/x64dbg-Plugin-SDK-For-x64-Assembler


Overview
--------

A plugin to allow searching for API calls and/or searching online from command bar


Features
--------

- Search online for API calls in the dissassembly window (lines that begin with 'call')
- Search from the command bar using google, msdn or pinvoke, ie: 'google <searchterm>'
- Open web browser to google, msdn or pinvoke, ie: 'msdn' opens browser at msdn.microsoft.com


Notes
-----

- 13/01/2017 fixed a bug with PLUG_MENUENTRY structure in the SDK and recompiled
- 26/06/2016 Updated x64dbg SDK for masm to version 1.0.0.3 and recompiled plugin.
- 01/03/2016 Updated x64dbg SDK for masm to version 1.0.0.2 and recompiled plugin.
- Added function APISearchLoadMenuIcon to load png resource image as raw bytes 
- Added menu icon for plugin (uses _plugin_menuseticon)
- Added menu entry icons for google, msdn and pinvoke (uses _plugin_menuentryseticon) 


================================================
File: APISearch.Asm
================================================
;=====================================================================================
; x64dbg plugin SDK for Masm - fearless 2017
;
; APISearch.asm
;
;-------------------------------------------------------------------------------------
.686
.MMX
.XMM
.x64

option casemap : none
option win64 : 11
option frame : auto
option stackbase : rsp

_WIN64 EQU 1
WINVER equ 0501h

;DEBUG64 EQU 1

IFDEF DEBUG64
    PRESERVEXMMREGS equ 1
    includelib \JWasm\lib\x64\Debug64.lib
    DBG64LIB equ 1
    DEBUGEXE textequ <'\Jwasm\bin\DbgWin.exe'>
    include \JWasm\include\debug64.inc
    .DATA
    RDBG_DbgWin	DB DEBUGEXE,0    
ENDIF


Include x64dbgpluginsdk.inc               ; Main x64dbg Plugin SDK for your program, and prototypes for the main exports 

Include APISearch.inc                   ; plugin's include file

pluginit	        PROTO :QWORD            ; Required prototype and export for x64dbg plugin SDK
plugstop            PROTO                   ; Required prototype and export for x64dbg plugin SDK
plugsetup           PROTO :QWORD            ; Required prototype and export for x64dbg plugin SDK
;=====================================================================================


.CONST
PLUGIN_VERSION      EQU 1

.DATA
align 01
PLUGIN_NAME         DB "APISearch",0

.DATA?
;-------------------------------------------------------------------------------------
; GLOBAL Plugin SDK variables
;-------------------------------------------------------------------------------------
align 08

PUBLIC              pluginHandle
PUBLIC              hwndDlg
PUBLIC              hMenu
PUBLIC              hMenuDisasm
PUBLIC              hMenuDump
PUBLIC              hMenuStack

pluginHandle        DD ?
hwndDlg             DQ ?
hMenu               DD ?
hMenuDisasm         DD ?
hMenuDump           DD ?
hMenuStack          DD ?


;-------------------------------------------------------------------------------------


.CODE

;=====================================================================================
; Main entry function for a DLL file  - required.
;-------------------------------------------------------------------------------------
DllMain PROC hInst:HINSTANCE, fdwReason:DWORD, lpvReserved:LPVOID
    .IF fdwReason == DLL_PROCESS_ATTACH
        mov rax, hInst
        mov hInstance, rax
    .ENDIF
    mov rax,TRUE
    ret
DllMain Endp


;=====================================================================================
; pluginit - Called by debugger when plugin.dp64 is loaded - needs to be EXPORTED
; 
; Arguments: initStruct - a pointer to a PLUG_INITSTRUCT structure
;
; Notes:     you must fill in the pluginVersion, sdkVersion and pluginName members. 
;            The pluginHandle is obtained from the same structure - it may be needed in
;            other function calls.
;
;            you can call your own setup routine from within this function to setup 
;            menus and commands, and pass the initStruct parameter to this function.
;
;-------------------------------------------------------------------------------------
pluginit PROC FRAME USES RBX initStruct:QWORD
    mov rbx, initStruct

    ; Fill in required information of initStruct, which is a pointer to a PLUG_INITSTRUCT structure
    mov eax, PLUGIN_VERSION
    mov [rbx].PLUG_INITSTRUCT.pluginVersion, eax
    mov eax, PLUG_SDKVERSION
    mov [rbx].PLUG_INITSTRUCT.sdkVersion, eax
    Invoke lstrcpy, Addr [rbx].PLUG_INITSTRUCT.pluginName, Addr PLUGIN_NAME
    
    mov rbx, initStruct
    mov eax, [rbx].PLUG_INITSTRUCT.pluginHandle
    mov pluginHandle, eax
    ; Do any other initialization here

	mov rax, TRUE
	ret
pluginit endp


;=====================================================================================
; plugstop - Called by debugger when the plugin.dp64 is unloaded - needs to be EXPORTED
;
; Arguments: none
; 
; Notes:     perform cleanup operations here, clearing menus and other housekeeping
;
;-------------------------------------------------------------------------------------
plugstop PROC FRAME
    ; remove any menus, unregister any callbacks etc
    Invoke _plugin_menuclear, hMenu
    mov eax, TRUE
    ret
plugstop endp


;=====================================================================================
; plugsetup - Called by debugger to initialize your plugins setup - needs to be EXPORTED
;
; Arguments: setupStruct - a pointer to a PLUG_SETUPSTRUCT structure
; 
; Notes:     setupStruct contains useful handles for use within x64dbg, mainly Qt 
;            menu handles (which are not supported with win32 api) and the main window
;            handle with this information you can add your own menus and menu items 
;            to an existing menu, or one of the predefined supported right click 
;            context menus: hMenuDisam, hMenuDump & hMenuStack
;            
;            plugsetup is called after pluginit. 
;-------------------------------------------------------------------------------------
plugsetup PROC FRAME USES RBX setupStruct:QWORD
    LOCAL hIconData:ICONDATA

    mov rbx, setupStruct

    ; Extract handles from setupStruct which is a pointer to a PLUG_SETUPSTRUCT structure  
    mov rax, [rbx].PLUG_SETUPSTRUCT.hwndDlg
    mov hwndDlg, rax
    mov eax, [rbx].PLUG_SETUPSTRUCT.hMenu
    mov hMenu, eax
    mov eax, [rbx].PLUG_SETUPSTRUCT.hMenuDisasm
    mov hMenuDisasm, eax
    mov eax, [rbx].PLUG_SETUPSTRUCT.hMenuDump
    mov hMenuDump, eax
    mov eax, [rbx].PLUG_SETUPSTRUCT.hMenuStack
    mov hMenuStack, eax
    
    ; Do any setup here: add menus, menu items, callback and commands etc
    Invoke _plugin_menuaddentry, hMenuDisasm, MENU_SEARCH_GOOGLE, Addr szMenuSearchGoogle
    Invoke _plugin_menuaddentry, hMenuDisasm, MENU_SEARCH_MSDN, Addr szMenuSearchMSDN
    Invoke _plugin_menuaddentry, hMenuDisasm, MENU_SEARCH_PINVOKE, Addr szMenuSearchPinvoke

    Invoke APISearchLoadMenuIcon, IMG_APISEARCH, Addr hIconData
    .IF eax == TRUE
        Invoke _plugin_menuseticon, hMenuDisasm, Addr hIconData
    .ENDIF
    
    Invoke APISearchLoadMenuIcon, IMG_SEARCHGOOGLE, Addr hIconData
    .IF eax == TRUE
        Invoke _plugin_menuentryseticon, pluginHandle, MENU_SEARCH_GOOGLE, Addr hIconData
    .ENDIF
    
    Invoke APISearchLoadMenuIcon, IMG_SEARCHMSDN, Addr hIconData
    .IF eax == TRUE
        Invoke _plugin_menuentryseticon, pluginHandle, MENU_SEARCH_MSDN, Addr hIconData
    .ENDIF
    
    Invoke APISearchLoadMenuIcon, IMG_SEARCHPINVOKE, Addr hIconData
    .IF eax == TRUE
        Invoke _plugin_menuentryseticon, pluginHandle, MENU_SEARCH_PINVOKE, Addr hIconData
    .ENDIF
    
    ; register commands for searching google, msdn and pinvoke
    Invoke _plugin_registercommand, pluginHandle, Addr szGoogle, Addr cbSearchGoogle, FALSE
    Invoke _plugin_registercommand, pluginHandle, Addr szMSDN, Addr cbSearchMSDN, FALSE
    Invoke _plugin_registercommand, pluginHandle, Addr szPinvoke, Addr cbSearchPinvoke, FALSE

    Invoke GuiAddLogMessage, Addr szAPISearchInfo        
    mov rax,TRUE
    ret
plugsetup endp


;=====================================================================================
; CBMENUENTRY - Called by debugger when a menu item is clicked - needs to be EXPORTED
;
; Arguments: cbType
;            cbInfo - a pointer to a PLUG_CB_MENUENTRY structure. The hEntry contains 
;            the resource id of menu item identifiers
;  
; Notes:     hEntry can be used to determine if the user has clicked on your plugins
;            menu item(s) and to do something in response to it.
;            Needs to be PROC C type procedure call to be compatible with debugger
;-------------------------------------------------------------------------------------
CBMENUENTRY PROC FRAME USES RBX cbType:QWORD, cbInfo:QWORD
    mov rbx, cbInfo
    xor rax, rax
    mov eax, [rbx].PLUG_CB_MENUENTRY.hEntry
    
    ;PrintDWORD eax
    
    .IF rax == MENU_SEARCH_GOOGLE
        Invoke SearchForAPIKeyword, 0 ;testDisasm
    
    .ELSEIF rax == MENU_SEARCH_MSDN
        Invoke SearchForAPIKeyword, 1 ;testDisasm
    
    .ELSEIF rax == MENU_SEARCH_PINVOKE
        Invoke SearchForAPIKeyword, 2 ; pinvoke
    .ENDIF    
    mov rax,TRUE
    ret
CBMENUENTRY endp


;=====================================================================================
; Search Google
;-------------------------------------------------------------------------------------
cbSearchGoogle PROC FRAME USES RBX argc:QWORD, argv:QWORD
    LOCAL dwSearchTerm:QWORD
    
    .IF argc == 1 ; just launch google if google is only thing typed
        Invoke GuiAddLogMessage, Addr szOpeningGoogle
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szGoogleHomeAddress
    .ELSE
        mov rbx, argv
        add rbx, 8d ; argv +1
        mov rax, [rbx] ; get pointer at argv[1] address to point to argv[1] string
        mov dwSearchTerm, rax
    
        Invoke lstrcpy, Addr szLogMsg, Addr szGoogleSearchingFor
        Invoke lstrcat, Addr szLogMsg, dwSearchTerm
        Invoke lstrcat, Addr szLogMsg, Addr szCRLF
        Invoke GuiAddLogMessage, Addr szLogMsg
        
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szGoogleSearchUrl
        Invoke lstrcat, Addr szWebSearchKeyword, dwSearchTerm
        
    .ENDIF
    Invoke ShellExecute, Addr szOpen, NULL, Addr szWebSearchKeyword, NULL, NULL, SW_SHOWNORMAL
    mov rax,TRUE
    ret
cbSearchGoogle endp


;=====================================================================================
; Search MSDN
;-------------------------------------------------------------------------------------
cbSearchMSDN PROC FRAME USES RBX argc:QWORD, argv:QWORD
    LOCAL dwSearchTerm:QWORD
    
    .IF argc == 1 ; just launch google if google is only thing typed
        Invoke GuiAddLogMessage, Addr szOpeningMSDN
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szMSDNHomeAddress
    .ELSE
        mov rbx, argv
        add rbx, 8d ; argv +1
        mov rax, [rbx] ; get pointer at argv[1] address to point to argv[1] string
        mov dwSearchTerm, rax
    
        Invoke lstrcpy, Addr szLogMsg, Addr szMSDNSearchingFor
        Invoke lstrcat, Addr szLogMsg, dwSearchTerm
        Invoke lstrcat, Addr szLogMsg, Addr szCRLF
        Invoke GuiAddLogMessage, Addr szLogMsg
        
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szMSDNSearchUrl
        Invoke lstrcat, Addr szWebSearchKeyword, dwSearchTerm
        
    .ENDIF
    Invoke ShellExecute, Addr szOpen, NULL, Addr szWebSearchKeyword, NULL, NULL, SW_SHOWNORMAL
    mov rax,TRUE
    ret
cbSearchMSDN endp


;=====================================================================================
; Search Pinvoke
;-------------------------------------------------------------------------------------
cbSearchPinvoke PROC FRAME USES RBX argc:QWORD, argv:QWORD
    LOCAL dwSearchTerm:QWORD
    
    .IF argc == 1 ; just launch google if google is only thing typed
        Invoke GuiAddLogMessage, Addr szOpeningPinvoke
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szPinvokeHomeAddress
    .ELSE
        mov rbx, argv
        ;mov rax, [rbx]
        ;Invoke MessageBox, hwndDlg, Addr szMenuPlugin1, rax, MB_OK
        
        add rbx, 8d ; argv +1
        mov rax, [rbx] ; get pointer at argv[1] address to point to argv[1] string
        mov dwSearchTerm, rax
    
        Invoke lstrcpy, Addr szLogMsg, Addr szPinvokeSearchingFor
        Invoke lstrcat, Addr szLogMsg, dwSearchTerm
        Invoke lstrcat, Addr szLogMsg, Addr szPinvokeNamespace
        Invoke lstrcat, Addr szLogMsg, Addr szCRLF
        Invoke GuiAddLogMessage, Addr szLogMsg
        
        Invoke lstrcpy, Addr szWebSearchKeyword, Addr szPinvokeSearchUrl
        Invoke lstrcat, Addr szWebSearchKeyword, dwSearchTerm
        Invoke lstrcat, Addr szWebSearchKeyword, Addr szPinvokeNamespace
        
    .ENDIF
    Invoke ShellExecute, Addr szOpen, NULL, Addr szWebSearchKeyword, NULL, NULL, SW_SHOWNORMAL
    mov rax,TRUE
    ret
cbSearchPinvoke endp


;=====================================================================================
; Search online for API keyword using specified search provider
;-------------------------------------------------------------------------------------
SearchForAPIKeyword PROC FRAME USES RBX RDI RSI dwSearchProvider:QWORD ; google = 0, msdn = 1, pinvoke = 3
    LOCAL sel:SELECTIONDATA
    LOCAL lenDisasmText:DWORD
    ;LOCAL tester:DWORD
    
    ;PrintText 'SearchForAPIKeyword'
    
    Invoke GuiSelectionGet, GUI_DISASSEMBLY, Addr sel
    Invoke GuiGetDisassembly, sel.start, Addr szDisasmText
    
    ;PrintString szDisasmText
    
    ;lea eax, szDisasmText
    ;DumpMem eax, 100
    ;mov eax, 28d
    ;PrintDec eax
    ;mov dwTester1, 27d
    ;PrintDec dwTester1
    ;mov eax, dwTester1
    ;mov DbgVar, eax
    ;PrintDec DbgVar
    ;mov tester, 26d
    ;PrintDec tester
    
    Invoke lstrlen, Addr szDisasmText
    mov lenDisasmText, eax

    lea rbx, szDisasmText
    mov eax, dword ptr [rbx]
    .IF eax == 'llac' ; good to go
        ;PrintDec eax
        ; strip out call and <> brackets and @ param stuff
        
        lea rsi, szDisasmText
        lea rdi, szAPISearchKeyword
        
        movzx eax, byte ptr [rsi]
        .WHILE al != '.' && al != '&' ; 64bit have & in the api calls, so to check for that as well
            .IF al == 0h
                Invoke GuiAddLogMessage, Addr szCouldNotFindAPI
                mov rax, FALSE
                ret
            .ENDIF
            inc rsi
            movzx eax, byte ptr [rsi]
        .ENDW

        inc rsi ; jump over the . and the first _ if its there
        movzx rax, byte ptr [rsi]
        .IF al == '_'
            inc rsi
        .ENDIF
        
        movzx eax, byte ptr [rsi]
        .WHILE al != '@' && al != '>'
            .IF al == 0h
                Invoke GuiAddLogMessage, Addr szCouldNotFindAPI
                mov rax, FALSE
                ret
            .ENDIF
            mov byte ptr [rdi], al
            inc rdi
            inc rsi
            movzx eax, byte ptr [rsi]
        .ENDW
        mov byte ptr [rdi], 0h ; null out string
        
        ; check if 2nd last byte was lowercase, if so if the last byte is 'A' or 'W' we strip that off - ansi/unicode part
        dec rdi
        dec rdi
        movzx eax, byte ptr [rdi]
        .IF al >= 'a' && al <= 'z'
            inc rdi
            movzx eax, byte ptr [rdi]
            .IF al == 'A' || al == 'W'
                mov byte ptr [rdi], 0h ; null out string
            .ENDIF
        .ENDIF 
        
        Invoke lstrcpy, Addr szLogMsg, Addr szSearchingForAPI
        Invoke lstrcat, Addr szLogMsg, Addr szAPISearchKeyword
        Invoke lstrcat, Addr szLogMsg, Addr szCRLF
        Invoke GuiAddLogMessage, Addr szLogMsg
        .IF dwSearchProvider == 0
            Invoke lstrcpy, Addr szWebSearchKeyword, Addr szGoogleSearchUrl
        
        .ELSEIF dwSearchProvider == 1
            Invoke lstrcpy, Addr szWebSearchKeyword, Addr szMSDNSearchUrl
        
        .ELSEIF dwSearchProvider == 2
            Invoke lstrcpy, Addr szWebSearchKeyword, Addr szPinvokeSearchUrl
        .ENDIF
        Invoke lstrcat, Addr szWebSearchKeyword, Addr szAPISearchKeyword
        
        .IF dwSearchProvider == 2
            Invoke lstrcat, Addr szWebSearchKeyword, Addr szPinvokeNamespace
        .ENDIF
        
        Invoke ShellExecute, Addr szOpen, NULL, Addr szWebSearchKeyword, NULL, NULL, SW_SHOWNORMAL
        mov rax, TRUE
    .ELSE
        Invoke GuiAddLogMessage, Addr szCanOnlySearchForAPI
        ret
    .ENDIF
    ret
SearchForAPIKeyword endp


;=====================================================================================
; APISearchLoadMenuIcon - Loads RT_RCDATA png resource and assigns it to ICONDATA
; Returns TRUE in eax if succesful or FALSE otherwise.
;-------------------------------------------------------------------------------------
APISearchLoadMenuIcon PROC FRAME USES RBX dqImageResourceID:QWORD, lpIconData:QWORD
    LOCAL hRes:QWORD
    
    ; Load image for our menu item
    Invoke FindResource, hInstance, dqImageResourceID, RT_RCDATA ; load png image as raw data
    .IF eax != NULL
        mov hRes, rax
        Invoke SizeofResource, hInstance, hRes
        .IF rax != 0
            mov rbx, lpIconData
            mov [rbx].ICONDATA.size_, rax
            Invoke LoadResource, hInstance, hRes
            .IF rax != NULL
                Invoke LockResource, rax
                .IF rax != NULL
                    mov rbx, lpIconData
                    mov [rbx].ICONDATA.data, rax
                    mov rax, TRUE
                .ELSE
                    ;PrintText 'Failed to lock resource'
                    mov rax, FALSE
                .ENDIF
            .ELSE
                ;PrintText 'Failed to load resource'
                mov rax, FALSE
            .ENDIF
        .ELSE
            ;PrintText 'Failed to get resource size'
            mov rax, FALSE
        .ENDIF
    .ELSE
        ;PrintText 'Failed to find resource'
        mov rax, FALSE
    .ENDIF    
    ret

APISearchLoadMenuIcon ENDP



;;=====================================================================================
;; Plugin Dialog Procedure
;;-------------------------------------------------------------------------------------
;PluginDlgProc PROC FRAME hWin:HWND, uMsg:UINT, wParam:WPARAM, lParam:LPARAM
;    
;    mov eax, uMsg
;    .IF eax == WM_INITDIALOG
;        ; Any initialization here
;        
;	.ELSEIF eax == WM_CLOSE
;        Invoke EndDialog, hWin, NULL
;        
;	.ELSEIF eax == WM_COMMAND
;        mov rax, wParam
;        and rax, 0FFFFh
;        .IF rax == IDC_PLUGINDLG_OK
;            Invoke SendMessage, hWin, WM_CLOSE, NULL, NULL
;        .ENDIF
;    .ELSE
;        mov rax, FALSE
;        ret
;	.ENDIF
;    mov rax, TRUE
;    ret
;PluginDlgProc endp


END DllMain



















================================================
File: APISearch.Def
================================================
;--------------------------------------------------------------------------------------------------------
; x64dbg plugin SDK for Masm32 - fearless 2017
;
; Export definition file for your plugin
;--------------------------------------------------------------------------------------------------------

LIBRARY APISearch
EXPORTS ; Main plugin exports - only pluginit is required, but others can be included for ease of use.
        ;
        pluginit
        plugstop
        plugsetup
        ;
        ; Plugin callbacks - only export (uncomment) the callbacks you are using in your plugin, comment out the others.
        ;
        ;CBINITDEBUG
        ;CBSTOPDEBUG
        ;CBCREATEPROCESS
        ;CBEXITPROCESS
        ;CBCREATETHREAD
        ;CBEXITTHREAD
        ;CBSYSTEMBREAKPOINT
        ;CBLOADDLL
        ;CBUNLOADDLL
        ;CBOUTPUTDEBUGSTRING
        ;CBEXCEPTION
        ;CBBREAKPOINT
        ;CBPAUSEDEBUG
        ;CBRESUMEDEBUG
        ;CBSTEPPED
        ;CBATTACH
        ;CBDETACH
        ;CBDEBUGEVENT
        CBMENUENTRY
        ;CBWINEVENT
        ;CBWINEVENTGLOBAL



================================================
File: APISearch.Inc
================================================
;=====================================================================================
; x64dbg plugin SDK for Masm - fearless 2017
;
; APISearch.inc
;
;-------------------------------------------------------------------------------------
include windows.inc
include CommCtrl.inc
include shellapi.inc

includelib user32.lib
includelib kernel32.lib
includelib gdi32.lib
includelib comctl32.lib
includelib shell32.lib

;PluginDlgProc           PROTO :HWND, :UINT, :WPARAM, :LPARAM
SearchForAPIKeyword     PROTO :QWORD
cbSearchGoogle          PROTO :QWORD, :QWORD
cbSearchMSDN            PROTO :QWORD, :QWORD
cbSearchPinvoke         PROTO :QWORD, :QWORD
APISearchLoadMenuIcon   PROTO :QWORD, :QWORD

.CONST
CRLF                    TEXTEQU <13,10,0> ; carriage return and linefeed for strings that require them (GuiAddLogMessage for example) 

IMG_APISEARCH           EQU 101 ; PNG image for plugin menu icon
IMG_SEARCHGOOGLE        EQU 102 ; PNG image for plugin menu icon
IMG_SEARCHMSDN          EQU 103 ; PNG image for plugin menu icon
IMG_SEARCHPINVOKE       EQU 104 ; PNG image for plugin menu icon

MENU_SEARCH_GOOGLE      EQU 1
MENU_SEARCH_MSDN        EQU 2
MENU_SEARCH_PINVOKE     EQU 3

;MENU_PLUGIN1            EQU 1
;IDD_PluginDlg           EQU 1000
;IDC_PLUGINDLG_OK        EQU 1001

.DATA
align 01
;szMenuPlugin1           DB "APISearch",0
;szPluginLoaded          DB "APISearch loaded.",CRLF
;szPluginUnloaded        DB "APISearch unloaded.",CRLF
szMenuSearchGoogle      DB "Search Google...",0
szMenuSearchMSDN        DB "Search MSDN...",0
szMenuSearchPinvoke     DB "Search Pinvoke...",0
szAPISearchInfo         DB 13,10         
                        DB "APISearch x64dbg plugin by fearless 2017 - www.LetTheLight.in",13,10
                        DB 13,10
                        DB "APISearch Features & Usage:",13,10
                        DB " - Search online for API calls in the dissassembly window (lines that begin with 'call')",13,10  
                        DB " - Search from the command bar using google, msdn or pinvoke, ie: 'google <searchterm>' ",13,10
                        DB " - Open web browser to google, msdn or pinvoke, ie: 'msdn' opens browser at msdn.microsoft.com",13,10
                        DB 13,10,0

; status messages
szDebuggingRequired     DB "APISearch: you need to be debugging to use this option.",CRLF
szCanOnlySearchForAPI   DB "APISearch: Can only search for API calls, make sure you have selected a line that starts with 'call'",CRLF
szCouldNotFindAPI       DB "APISearch: Could not find an API function call in the selected line.",CRLF
szSearchingForAPI       DB "APISearch: Searching for API function: ",0
szOpeningGoogle         DB "Opening www.google.com in a web browser.",CRLF
szOpeningMSDN           DB "Opening msdn.microsoft.com in a web browser.",CRLF
szOpeningPinvoke        DB "Opening www.pinvoke.net in a web browser.",CRLF
szLogMsg                DB 1024 DUP (0)

; search strings
szGoogleHomeAddress     DB "http://www.google.com",0
szMSDNHomeAddress       DB "http://msdn.microsoft.com",0
szPinvokeHomeAddress    DB "http://www.pinvoke.net",0
szGoogleSearchUrl       DB "https://www.google.com/search?q=",0
szMSDNSearchUrl         DB "https://social.msdn.microsoft.com/search/en-us/?query=",0
szPinvokeSearchUrl      DB "http://www.pinvoke.net/search.aspx?search=",0
szPinvokeNamespace      DB "&namespace=[All]",0
szGoogleSearchingFor    DB "Google search: ",0
szMSDNSearchingFor      DB "MSDN search: ",0
szPinvokeSearchingFor   DB "Pinvoke search: ",0
szWebSearchKeyword      DB MAX_PATH DUP (0)
szAPISearchKeyword      DB MAX_PATH DUP (0)

; Misc
szDisasmText            DB GUI_MAX_DISASSEMBLY_SIZE DUP (0)
szOpen                  DB "Open",0 
szCRLF                  DB 13,10,0

; registered command strings
szGoogle                DB "Google",0
szMSDN                  DB "MSDN",0
szPinvoke               DB "Pinvoke",0



.DATA?
align 08
hInstance               HINSTANCE ?
;dwTester1               DD ?

IFDEF DEBUG64
    DbgVar DD ?
ENDIF


================================================
File: APISearch.dlg
================================================
[Non-text file]


================================================
File: APISearch.rap
================================================
[Project]
Assembler=JWasm
Type=Dll64 Project
Description=APISearch
Backup=$P\Bak\
Group=1
GroupExpand=1
Res.rc=1
Debug=0
Ver.rc=1
[Files]
1=APISearch.Asm
2=APISearch.Inc
3=APISearch.Def
4=APISearch.rc
5=
6=APISearch-readme.txt
[MakeFiles]
0=APISearch.rap
1=APISearch.rc
2=APISearch.asm
3=APISearch.obj
4=APISearch.res
5=APISearch.exe
6=APISearch.def
7=APISearch.dll
8=APISearch.txt
9=APISearch.lib
10=APISearch.mak
11=APISearch.hla
12=APISearch.com
13=APISearch.ocx
14=APISearch.idl
15=APISearch.tlb
16=APISearch.sys
17=APISearch.dp64
18=APISearch.pdb
[MakeDef]
Menu=1,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0
1=4,O,$B\RC.EXE /v,1
2=3,O,$B\JWASM64.EXE /c -win64 -Zp8 /Zi /win64 /D_WIN64 /Cp /nologo /W2 /I"$I",2
3=7,O,$B\LINK.EXE /SUBSYSTEM:WINDOWS /RELEASE /DLL /DEF:$6 /MACHINE:X64 /LIBPATH:"$L" /OUT:"$17",3,4
4=0,0,,5
5=rsrc.obj,O,$B\CVTRES.EXE,rsrc.res
6=*.obj,O,$B\JWASM64.EXE /c -win64 -Zp8 /Zi /win64 /D_WIN64 /Cp /nologo /W2 /I"$I",*.asm
7=0,0,"$E\x64\x64dbg",5
11=4,O,$B\RC.EXE /v,1
12=3,O,$B\JWASM64.EXE /c -win64 -Zp8 /Zi /win64 /D_WIN64 /Cp /nologo /W2 /Zi /Zd /nologo /I"$I",2
13=7,O,$B\LINK.EXE /SUBSYSTEM:WINDOWS /DEBUG /DEBUGTYPE:CV /PDB:"$18" /DLL /DEF:$6 /MACHINE:X64 /LIBPATH:"$L" /OUT:"$17",3,4
14=0,0,,5
15=rsrc.obj,O,$B\CVTRES.EXE,rsrc.res
16=*.obj,O,$B\JWASM64.EXE /c -win64 -Zp8 /Zi /win64 /D_WIN64 /Cp /nologo /W2 /I"$I",*.asm
17=0,0,"$E\x64\x64dbg",$.exe
[Resource]
1=,1,8,APISearch.xml
2=,101,7,Images\APISearch.png
3=,102,7,Images\search_google.png
4=,103,7,Images\search_msdn.png
5=,104,7,Images\search_pinvoke.png
[StringTable]
[Accel]
[VerInf]
Nme=VERINF1
ID=1
FV=1.0.0.4
PV=1.0.0.4
VerOS=0x00000004
VerFT=0x00000002
VerLNG=0x00000409
VerCHS=0x000004B0
ProductVersion=1.0.0.4
ProductName=APISearch
OriginalFilename=APISearch.dp64
LegalTrademarks=fearless (C) 2017 www.LetTheLight.in
LegalCopyright=fearless (C) 2017 www.LetTheLight.in
InternalName=APISearch.dll
FileDescription=APISearch plugin for x64dbg
FileVersion=1.0.0.4
CompanyName=fearless @ www.LetTheLight.in
[Group]
Group=Assembly,Resources,Misc
1=1
2=1
3=1
4=2
5=
6=3
7=
8=
9=
10=
11=
[AutoLoad]
AutoLoad=1,2,3
[RADebugBP]
1=
2=
0=
4=
[Find]
1="include"
2=":DWORD"
3="PrintText"
[Collapse]
1=2752528,
3=
2=
6=
4=
[Size]
1=0,0,0,0,131
3=0,0,0,0,152
2=0,0,0,0,131
5=
6=0,0,0,0,471
4=0,0,0,0,94
[VersionControl]
Settings=1279
Milestones=129
MilestoneEvery=10
MilestoneEveryCurrent=0
MilestoneOnBuild=0.0.0.0
MilestoneOnTime=2
MilestoneOnDate=0
MilestoneOnDateWhen=1
MilestoneOnDateStatus=0
MilestoneOnDateDate=13
MilestoneOnDateTimeYear=2015
MilestoneOnDateTimeMonth=4
MilestoneOnDateTimeDate=22
MilestoneOnDateTimeHour=15
MilestoneOnDateTimeMin=6
MilestoneOnDateTimeSec=40
MilestoneOnDateTimeStatus=0
BackupLocation=M:\radasm\JWasm\Projects\VCBackups\
CompressionLevel=0
DefaultComment=Project $N, $Z, Backup Created On $D At $T.
ExcludeExt1=\
ExcludeExt2=\
ExcludeExt3=\
ExcludeExt4=\
FileVerLength=4
FileVer2Range=0
FileVer3Range=0
FileVer4Range=0
ProductVerLength=4
ProductVer2Range=0
ProductVer3Range=0
ProductVer4Range=0
[PTimer]
PTimer=70228816
[GroupExpand]
GroupExpand=1,1,1,0
[BookMark]
0=
1=
2=
3=
4=
5=
6=
7=
8=
9=
[BreakPoint]
0=



================================================
File: APISearch.rc
================================================
;#include "Res/APISearchDlg.rc"
#include "Res/APISearchRes.rc"
#include "Res/APISearchVer.rc"



================================================
File: APISearch.xml
================================================
<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<assembly
    xmlns="urn:schemas-microsoft-com:asm.v1"
    manifestVersion="1.0">
    <assemblyIdentity
        version="1.0.0.0"
        processorArchitecture="amd64"
        name="Company.Product.Application"
        type="Win32"
    />
    <description>APISearch</description>
    <dependency>
        <dependentAssembly>
            <assemblyIdentity
                type="win32"
                name="Microsoft.Windows.Common-Controls"
                version="6.0.0.0"
                processorArchitecture="amd64"
                publicKeyToken="6595b64144ccf1df"
                language="*"
            />
        </dependentAssembly>
    </dependency>
</assembly>




================================================
File: release/APISearch-readme.txt
================================================
;=====================================================================================
;
; APISearch-readme.txt
;
; v1.0.0.4 - Last updated: 13/01/2017
;
;-------------------------------------------------------------------------------------

About
-----

APISearch Plugin (x64) For x64dbg (64bit plugin)
by fearless - www.LetTheLight.in

Created with the x64dbg Plugin SDK For x64 Assembler
https://github.com/mrfearless/x64dbg-Plugin-SDK-For-x64-Assembler


Overview
--------

A plugin to allow searching for API calls and/or searching online from command bar


Features
--------

- Search online for API calls in the dissassembly window (lines that begin with 'call')
- Search from the command bar using google, msdn or pinvoke, ie: 'google <searchterm>'
- Open web browser to google, msdn or pinvoke, ie: 'msdn' opens browser at msdn.microsoft.com


Notes
-----

- 13/01/2017 fixed a bug with PLUG_MENUENTRY structure in the SDK and recompiled
- 26/06/2016 Updated x64dbg SDK for masm to version 1.0.0.3 and recompiled plugin.
- 01/03/2016 Updated x64dbg SDK for masm to version 1.0.0.2 and recompiled plugin.
- Added function APISearchLoadMenuIcon to load png resource image as raw bytes 
- Added menu icon for plugin (uses _plugin_menuseticon)
- Added menu entry icons for google, msdn and pinvoke (uses _plugin_menuentryseticon) 


================================================
File: release/APISearch.dp64
================================================
[Non-text file]


================================================
File: res/APISearchDlg.rc
================================================
#define IDD_PluginDlg 1000
#define IDC_PLUGINDLG_OK 1001
IDD_PluginDlg DIALOGEX 6,6,189,99
CAPTION "Plugin Dialog"
FONT 8,"Segoe UI",400,0
STYLE 0x10CC0800
EXSTYLE 0x00000000
BEGIN
  CONTROL "Ok",IDC_PLUGINDLG_OK,"Button",0x50010000,56,71,78,17,0x00000000
END



================================================
File: res/APISearchRes.rc
================================================
#define MANIFEST 						24
1						MANIFEST  DISCARDABLE "APISearch.xml"
101						RCDATA    DISCARDABLE "Images/APISearch.png"
102						RCDATA    DISCARDABLE "Images/search_google.png"
103						RCDATA    DISCARDABLE "Images/search_msdn.png"
104						RCDATA    DISCARDABLE "Images/search_pinvoke.png"



================================================
File: res/APISearchVer.rc
================================================
#define VERINF1 1
VERINF1 VERSIONINFO
FILEVERSION 1,0,0,4
PRODUCTVERSION 1,0,0,4
FILEOS 0x00000004
FILETYPE 0x00000002
BEGIN
  BLOCK "StringFileInfo"
  BEGIN
    BLOCK "040904B0"
    BEGIN
      VALUE "CompanyName", "fearless @ www.LetTheLight.in\0"
      VALUE "FileVersion", "1.0.0.4\0"
      VALUE "FileDescription", "APISearch plugin for x64dbg\0"
      VALUE "InternalName", "APISearch.dll\0"
      VALUE "LegalCopyright", "fearless (C) 2017 www.LetTheLight.in\0"
      VALUE "LegalTrademarks", "fearless (C) 2017 www.LetTheLight.in\0"
      VALUE "OriginalFilename", "APISearch.dp64\0"
      VALUE "ProductName", "APISearch\0"
      VALUE "ProductVersion", "1.0.0.4\0"
    END
  END
  BLOCK "VarFileInfo"
  BEGIN
    VALUE "Translation", 0x0409, 0x04B0
  END
END


